//Set this constant to 1 to print to librarydebugger.
#Define_Constant librarydebug 0

digital_input Calcola;
analog_output FaseLunareOggi;
analog_output FaseLunareDomani;
analog_output FaseLunareDopodomani;

function main()
{
    waitForInitializationComplete();
}

long_integer_function check_for_leapyears(signed_long_integer startyear, signed_long_integer endyear)
{	
 	signed_long_integer i, ly, leapyears;

	leapyears = 0;

 	if(startyear > endyear)
	{
		i = startyear;
		startyear = endyear;
		endyear = i;		
	}

 	
 	for(i = startyear to endyear)
 	{
 		if(i mod 400 = 0) ly = 1;
  		else if(i mod 100 = 0) ly = 0;
  		else if(i mod 4 = 0) ly = 1; 
  		else ly = 0;
  		
  		if(ly = 1) leapyears = leapyears + 1;
 	}
 	
 	return(leapyears);
}

integer_function days_in_month(long_integer monthnum)
{	
	integer mlength;
	
	switch(monthnum) 
 	{
 		case(1): mlength = 31;
  		case(2): mlength = 28;
		case(3): mlength = 31;
		case(4): mlength = 30;
		case(5): mlength = 31;
		case(6): mlength = 30;
    	case(7): mlength = 31;
    	case(8): mlength = 31;
    	case(9): mlength = 30;
    	case(10): mlength = 31;
    	case(11): mlength = 30;
    	case(12): mlength = 31;
    }
    
    return(mlength);
}

long_integer_function days_from_zero(long_integer i_day, long_integer i_mon, signed_long_integer i_yr, integer bce)
{	
	integer i;
	long_integer monthdays;
	signed_long_integer dist;
	
    if(i_mon > 2) dist = (i_yr * 365) + check_for_leapyears(0, i_yr);
    else if(i_mon <= 2) dist = (i_yr * 365) + check_for_leapyears(0, i_yr - 1);
    
	if(bce)
	{
		dist = (dist * -1);
		if(librarydebug) print("We have a BCE value!");
    }
    else dist = dist - 367;
    
    if(librarydebug) print("Days from 1/1/1 to date, including leap years: %ld", dist);
  	  	
  	if(i_mon > 1)
  	{
  		monthdays = 0;
  		
  		for(i = 1 to i_mon - 1) monthdays = monthdays + days_in_month(i);
  		dist = dist + monthdays;
  		
  		if(librarydebug) print("Days in all of the months from the start of the year to date: %ld", monthdays);
  	}
  	
 	dist = dist + i_day;
 	
 	return(dist);
}

integer_function check_syntax(long_integer i_day, long_integer i_mon, long_integer i_yr)
{
 	integer i;
 	
 	i = 0;

 	if(i_yr = 0) i = 1;

 	if(i_mon > 12 || i_mon < 1) i = 2;

 	if(i_day < 1 || i_day > days_in_month(i_mon)) i = 3;
 	
 	return(i);
}

push Calcola
{	
 	signed_long_integer start[6], end[6], i, daysfrommonths, startdist, enddist, diff[4], diffminuti;
 	integer temperr, loc1, loc2, gl_err, oggi, domani, dopodomani;
 	string datediff[128];
	string startdate[128];
	string starttime[128];
	string enddate[128];
	string endtime[128];
	

	startdate = "1/17/1980";
	starttime = "21:18:00";
	enddate = date(1);
	endtime = "00:00:00";

	gl_err = 0;
	
	loc1 = find("/", startdate);
	loc2 = find("/", startdate, loc1 + 1);
 	start[0] = atol(mid(startdate, loc1 + 1, loc2 - loc1));
 	start[1] = atol(left(startdate, len(startdate) - loc1 + 1));
 	start[2] = atol(right(startdate, len(startdate) - loc2));
 	
 	if(librarydebug) print("Current start date values %ld/%ld/%ld and loc values %d - %d", start[1], start[0], start[2], loc1, loc2);
 	
	loc1 = find("/", enddate);
	loc2 = find("/", enddate, loc1 + 1);
 	end[0] = atol(mid(enddate, loc1 + 1, loc2 - loc1));
 	end[1] = atol(left(enddate, len(enddate) - loc1 + 1));
 	end[2] = atol(right(enddate, len(enddate) - loc2));

 	if(librarydebug) print("Current end date values %ld/%ld/%ld", end[1], end[0], end[2]);
 	
 	loc1 = find(":", starttime);
	loc2 = find(":", starttime, loc1 + 1);
 	start[3] = atol(left(starttime, len(starttime) - loc1 + 1));
 	start[4] = atol(mid(starttime, loc1 + 1, loc2 - loc1));
 	start[5] = atol(right(starttime, len(starttime) - loc2));
 	
 	if(librarydebug) print("Current start time values %ld:%ld:%ld and loc values %d - %d", start[3], start[4], start[5], loc1, loc2);
 	
	loc1 = find(":", endtime);
	loc2 = find(":", endtime, loc1 + 1);
 	end[3] = atol(left(endtime, len(endtime) - loc1 + 1));
 	end[4] = atol(mid(endtime, loc1 + 1, loc2 - loc1));
 	end[5] = atol(right(endtime, len(endtime) - loc2));

 	if(librarydebug) print("Current end time values %ld:%ld:%ld", end[3], end[4], end[5]);
 	
 	temperr = check_syntax(start[0], start[1], start[2]);
 	if(temperr != 0) gl_err = temperr;
 	temperr = check_syntax(end[0], end[1], end[2]) * 10;
 	if(temperr != 0) gl_err = temperr;
 	
  	startdist = days_from_zero(start[0], start[1], start[2], 0);
  	enddist = days_from_zero(end[0], end[1], end[2], 0);
 	
 	if(librarydebug) print("All days from 1/1/1 to start date: %ld", startdist);
 	if(librarydebug) print("All days from 1/1/1 to end date: %ld", enddist);
 	
 	
 	diff[0] = enddist - startdist;
 	diff[1] = end[3] - start[3];
 	diff[2] = end[4] - start[4];
 	diff[3] = end[5] - start[5];
 	
 	if(diff[3] < 0)
 	{
 		diff[3] = diff[3] + 60;
 		diff[2] = diff[2] - 1;
 	}
 	if(diff[2] < 0)
 	{
 		diff[2] = diff[2] + 60;
 		diff[1] = diff[1] - 1;
 	}
 	if(diff[1] < 0)
 	{
 		diff[1] = diff[1] + 24;
 		diff[0] = diff[0] - 1;
 	}
 	
 	
 	if(gl_err != 0)
 	{
 		if(librarydebug) print("******* ERROR *******");
 	}
 	else if(librarydebug) print("Difference is %ld", diff[0]);
 	
 	if(librarydebug) print("gl_error is %d", gl_err);
    
	switch(gl_err)
    {
		case(0): 
		{
			datediff = ltoa(diff[0]) + "." + ltoa(diff[1]) + "." + ltoa(diff[2]) + "." + ltoa(diff[3]);
			diffminuti = (diff[0] * 24) * 60;
			diffminuti = diffminuti + (diff[1] * 60) + diff[2];
			diffminuti = (diffminuti mod 42524) / (42524 / 24);
            diffminuti = (diffminuti + 1) mod 24;
            oggi = diffminuti;
            domani = diffminuti + 1;
            if(domani = 24) domani = 0;
            dopodomani = domani + 1;
			FaseLunareOggi = ATOSI(ltoa(oggi));
			FaseLunareDomani = ATOSI(ltoa(domani));
			FaseLunareDopodomani = ATOSI(ltoa(dopodomani));
		}			
		case(1): datediff = "Error: Bad starting year value (probably because there's no year 0)";
		case(2): datediff = "Error: Bad starting month value (out of range)";
		case(3): datediff = "Error: Bad starting day value (out of range)";
		case(4): datediff = "Error: That's not a real month!";
		case(10): datediff = "Error: Bad ending year value (probably because there's no year 0)";
		case(20): datediff = "Error: Bad ending month value (out of range)";
		case(30): datediff = "Error: Bad ending day value (out of range)";
		default: datediff = "Error: Unknown (how did you get this message?)";
	}
}
